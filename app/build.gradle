plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-kapt'
//    id 'io.objectbox'
    id 'com.microtech.plugins.resourcesId' version("1.0.0")
}

static def releaseTime() {
    return new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("GMT+8"))
}

android {
    compileSdk rootProject.compileSdkVersion.toInteger()

    defaultConfig {
        applicationId "com.microtech.aidexx"
        minSdk rootProject.minSdk.toInteger()
        targetSdk rootProject.targetSdk.toInteger()
        versionCode rootProject.appVersionCode as int
        versionName rootProject.appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "resourceVersion", "\"" + rootProject.resourceVersion + "\""

        ndk {
            // 设置支持的SO库架构
            abiFilters "armeabi-v7a", 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            buildConfigField("Boolean", "isPre", "false")
            buildConfigField("Boolean", "enableEncrypt", "false")
            buildConfigField("String", "baseUrl", "\"https://china.pancares.com\"")
            buildConfigField("String", "updateUrl", "\"https://china.pancares.com/\"")
            buildConfigField("String", "logUploadUrl", "\"https://china.pancares.com/backend/vcs/log/uploadLog/\"")
            buildConfigField("String", "welfareCenterUrl", "\"https://aidexh5-test.microtechmd.com/#/\"")
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    GETUI_APPID: ""
            ]
        }

        debug {
            debuggable true
            buildConfigField("Boolean", "isPre", "false")
            buildConfigField("Boolean", "enableEncrypt", "false")
            buildConfigField("String", "baseUrl", "\"http://192.168.222.26:5555/\"")
            buildConfigField("String", "updateUrl", "\"http://192.168.222.26:5555/\"")
            buildConfigField("String", "logUploadUrl", "\"https://china.pancares.com/backend/vcs/log/uploadLog/\"")
            buildConfigField("String", "welfareCenterUrl", "\"http://192.168.222.26:8000/#/\"")
            manifestPlaceholders = [
                    GETUI_APPID: "nwQJTPvYSL86k4zuAkbUC1"
            ]
        }
    }

    aaptOptions {
        File publicTxtFile = project.rootProject.file('app/dist/public.txt')
        if (publicTxtFile.exists()) {
            project.logger.error "${publicTxtFile} exists, apply it."
            aaptOptions.additionalParameters "--stable-ids", "${publicTxtFile.absolutePath}"
        } else {
            project.logger.error "${publicTxtFile} not exists"
//            aaptOptions.additionalParameters "--emit-ids", "${publicTxtFile.absolutePath}"
        }
    }

    flavorDimensions "AREA", "BRAND", "GLUCOSE_UNIT"
    productFlavors {
        cn {
            dimension "AREA"
        }
        gp {
            dimension "AREA"
        }


        medmmoltrust {
            dimension "BRAND"
            applicationIdSuffix ".medmmoltrust"
        }
        medmgtrust {
            dimension "BRAND"
            applicationIdSuffix ".medmgtrust"
        }
        aidex {
            dimension "BRAND"
        }


        mgmo {
            dimension "GLUCOSE_UNIT"
        }
        mg {
            dimension "GLUCOSE_UNIT"
            applicationIdSuffix ".mg"
        }
        mmol {
            dimension "GLUCOSE_UNIT"
            applicationIdSuffix ".mmol"
        }
    }

    variantFilter { variant ->
        def flavors = variant.flavors

        def area = flavors[0].name
        def brand = flavors[1].name
        def unit = flavors[2].name

        if (area == "cn") {
            //只保留cn aidex mg版本
            setIgnore(brand != "aidex" || unit != "mgmo")
        } else {
            // gp 没有cnmg版本
            if (unit == "mgmo") {
                setIgnore(true)
            }
            if (brand == "medmmoltrust") {
                if (unit == "mg") {
                    setIgnore(true)
                }
            } else if (brand == "medmgtrust") {
                if (unit == "mmol") {
                    setIgnore(true)
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "AiDEX-X_${variant.versionName}_${variant.flavorName}_${variant.buildType.name}_${releaseTime()}.apk"
                outputFileName = fileName
            }
        }
    }
}

dependencies {
    implementation project(path: ':chartlib')
    implementation project(path: ':blecomm')
    implementation project(path: ':cgat')
    implementation "androidx.fragment:fragment-ktx:1.6.0"
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.work:work-runtime:2.8.1'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'androidx.databinding:databinding-runtime:8.0.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'com.tencent:mmkv:1.3.0'
    implementation 'io.github.jeremyliao:live-event-bus-x:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation 'com.tencent.mars:mars-xlog:1.2.5'
    implementation 'com.tencent.bugly:crashreport:4.1.9'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    debugImplementation("io.objectbox:objectbox-android-objectbrowser:$objectboxVersion")
    implementation "io.objectbox:objectbox-kotlin:$objectboxVersion"
    releaseImplementation("io.objectbox:objectbox-android:$objectboxVersion")
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.retrofit2:adapter-java8:2.0.2"
    implementation "com.squareup.okhttp3:okhttp:4.10.0"
    implementation 'com.contrarywind:wheelview:4.1.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.haibin:calendarview:3.7.1'

//    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.5.1'
    implementation  'io.github.scwang90:refresh-layout-kernel:2.0.5'
    implementation  'io.github.scwang90:refresh-header-classics:2.0.5'    //经典刷新头

    implementation 'com.github.bumptech.glide:glide:4.13.2'
    implementation 'com.flyco.roundview:FlycoRoundView_Lib:1.1.4@aar'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.8.0"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'

    //阴影layout
    implementation 'com.github.lihangleo2:ShadowLayout:3.3.2'
    implementation "net.lingala.zip4j:zip4j:2.11.5"
    //侧滑删除
    implementation 'com.github.mcxtzhang:SwipeDelMenuLayout:V1.3.0'

    //个验sdk
    implementation 'com.getui:gtsdk:3.2.18.0'  //个推SDK
    implementation 'com.getui:gtc:3.2.5.0'  //个推核心组件

//    implementation 'com.getui.opt:hwp:3.1.1'   // 华为
//    implementation 'com.getui.opt:xmp:3.2.1'   // 小米
//    implementation 'com.assist-v3:oppo:3.1.2'  // oppo
//    implementation 'com.assist-v3:vivo:3.1.0'  // vivo
//    implementation 'com.getui.opt:ups:3.0.3'   // ups，ups目前支持坚果，索尼，海信手机
//    implementation 'com.huawei.hms:push:6.5.0.300'
}

apply plugin: 'io.objectbox'
